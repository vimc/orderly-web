buildscript {
    repositories {
        mavenCentral()
        // This is needed for gradle-docker
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

def api_docker_version = 'UNKNOWN'
if (hasProperty('docker_version')) {
    api_docker_version = docker_version
}
def api_docker_name = 'montagu-reporting-api'
if (hasProperty('docker_name')) {
    api_docker_name = docker_name
}

apply plugin: 'application'
apply plugin: 'docker'

mainClassName = "org.vaccineimpact.reporting_api.app_start.SparkAppKt"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "com.sparkjava:spark-core:2.5.4"
    compile "org.slf4j:slf4j-simple:1.7.21"

    compile "com.google.code.gson:gson:2.2.4"
    compile "com.github.salomonbrys.kotson:kotson:2.5.0"
    compile "com.opencsv:opencsv:3.9"

    compile "org.postgresql:postgresql:9.4.1212.jre7"
    compile "org.jooq:jooq:3.9.1"
    compile "org.jooq:jooq-meta:3.9.1"

    compile "org.pac4j:spark-pac4j:2.0.0"
    compile "org.pac4j:pac4j-http:2.0.0"

    testCompile "org.slf4j:slf4j-simple:1.7.21"
    testCompile "khttp:khttp:0.1.0"
    testCompile "com.beust:klaxon:0.31"
    testCompile "com.opencsv:opencsv:3.9"

    testCompile "com.github.fge:json-schema-validator:2.2.6"

    compile project(":databaseInterface")
    compile project(":models")
    compile project(":security")

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.nhaarman:mockito-kotlin:1.3.0"
    testCompile "org.assertj:assertj-core:3.6.2"
    testCompile "com.beust:klaxon:0.31"
    testCompile project(":testHelpers")
}

docker {
    baseImage = "openjdk:8u121-jre"
}

distDocker {
    dependsOn = [build]
    push = true
    tag = api_docker_name
    tagVersion = api_docker_version
}

// creates folder at app/demo with up to date orderly test data
task generateTestData() {
    doLast {
        String image = "docker.montagu.dide.ic.ac.uk:5000/orderly:master"

        def username = System.properties['user.name']
        def uid = ["id", "-u", username].execute().text.trim()

        exec {
            commandLine 'docker', 'pull', image
        }
        exec {
            commandLine 'docker', 'run', '--rm', '--entrypoint', 'create_orderly_demo.sh', '-u', uid, '-v', "$projectDir:/orderly", '-w', "/orderly", image, "demo"
        }
    }
}

// removes the app/demo folder
task cleanUpTestData() {
    doLast {

        exec {
            commandLine "rm", "$projectDir/demo", "--recursive"
        }
    }
}

task copySpec(type: Copy) {
    from 'src/test/resources/spec'
    include '*.schema.json'
    into 'build/classes/test/spec'
    outputs.upToDateWhen { false }
}
test.dependsOn 'copySpec'

task checkConfigDirExists() {
    doLast {
        def dir = file("/etc/montagu/reports_api/token_key")
        if (!dir.exists() || !dir.canWrite()) {
            throw new Exception("Cannot write to ${dir.path}. " +
                    "Please create it and make sure the user running the tests has write permissions. " +
                    "e.g. sudo mkdir -m 777 -p /etc/montagu/reports_api/token_key")
        }
    }
}
test.dependsOn 'checkConfigDirExists'

// We run the integration test as a suite (see APITests); this line prevents
// them being run individually as well
test.exclude "**/integration_tests/tests/**"

copyConfig.finalizedBy 'showConfig'
copyTestConfig.finalizedBy 'showTestConfig'