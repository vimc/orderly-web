buildscript {
    repositories {
        mavenCentral()
        // This is needed for gradle-docker
        jcenter()
    }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

def api_docker_version = 'UNKNOWN'
if (hasProperty('docker_version')) {
    api_docker_version = docker_version
}
def api_docker_name = 'orderly-web'
if (hasProperty('docker_name')) {
    api_docker_name = docker_name
}

apply plugin: 'application'
apply plugin: 'docker'

mainClassName = "org.vaccineimpact.orderlyweb.app_start.SparkAppKt"

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "com.sparkjava:spark-core:2.7.1"
    compile "com.sparkjava:spark-template-freemarker:2.7.1"
    compile "org.slf4j:slf4j-simple:1.7.21"

    compile "com.google.code.gson:gson:2.2.4"
    compile "com.github.salomonbrys.kotson:kotson:2.5.0"
    compile "com.opencsv:opencsv:3.9"

    compile "org.apache.commons:commons-lang3:3.12.0"
    compile "org.postgresql:postgresql:9.4.1212.jre7"
    compile "org.jooq:jooq:3.9.1"
    compile "org.jooq:jooq-meta:3.9.1"

    compile "org.pac4j:spark-pac4j:3.0.0"
    compile "org.pac4j:pac4j-http:3.2.0"
    compile 'org.pac4j:pac4j-jwt:3.2.0'
    compile 'org.pac4j:pac4j-oauth:3.6.1'
    compile 'commons-codec:commons-codec:1.10'

    compile "org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5"

    compile "org.json:json:20201115"
    compile "com.squareup.okhttp3:okhttp:4.8.1"

    compile "org.ocpsoft.prettytime:prettytime:4.0.5.Final"

    testCompile "org.slf4j:slf4j-simple:1.7.21"
    testCompile "com.beust:klaxon:0.31"
    testCompile "com.opencsv:opencsv:3.9"

    testCompile "com.github.fge:json-schema-validator:2.2.6"

    compile project(":databaseInterface")

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:2.+"
    testCompile "com.nhaarman:mockito-kotlin:1.3.0"
    testCompile "org.assertj:assertj-core:3.6.2"
    testCompile "com.beust:klaxon:0.31"
    testCompile "net.sourceforge.htmlunit:htmlunit:2.4"
    testCompile "org.xmlmatchers:xml-matchers:0.10"
    testCompile "org.jsoup:jsoup:1.11.3"
    testImplementation "org.hamcrest:hamcrest:2.1"
    testCompile project(":testHelpers")
}

docker {
    baseImage = "orderly-web-dist-base"
}

distDocker {
    dependsOn = [build]
    tag = api_docker_name
    tagVersion = api_docker_version
}

// creates folder at app/demo with up to date orderly test data
task generateTestData() {
    doLast {
        String image = "vimc/orderly:master"
        def username = System.properties['user.name']
        def uid = ["id", "-u", username].execute().text.trim()

        println("Pulling " + image)

        exec {
            commandLine 'docker', 'pull', image
        }
        exec {
            commandLine 'docker', 'run', '--rm', '--entrypoint', 'create_orderly_demo.sh', '-u', uid, '-v', "$projectDir:/orderly", '-w', "/orderly", image, "."
        }
        
        String owMigrateImage = "vimc/orderlyweb-migrate:master"
        println("Pulling " + owMigrateImage)
        exec {
            commandLine 'docker', 'pull', owMigrateImage
        }
        exec {
            commandLine 'docker', 'run', '--rm', '-v', "$projectDir/demo:/orderly", owMigrateImage
        }
        exec {
            commandLine 'docker', 'run', '--rm', '-v', "$projectDir/git:/orderly", owMigrateImage
        }
    }
}

// removes the app/demo folder
task cleanUpTestData() {
    doLast {
        exec {
            commandLine "rm", "$projectDir/demo", "--recursive"
        }
        exec {
            commandLine "rm", "$projectDir/git", "--recursive"
        }
    }
}

task copySpec(type: Copy) {
    from 'src/test/resources/spec'
    include '*.schema.json'
    into 'build/classes/test/spec'
    outputs.upToDateWhen { false }
}
test.dependsOn 'copySpec'

task compileFrontEnd() {
    doLast {
        exec {
            commandLine "npm", "run", "build", "--prefix=$projectDir/static/"
        }
    }
}

processResources.dependsOn 'compileFrontEnd'

copyConfig.finalizedBy 'showConfig'
copyTestConfig.finalizedBy 'showTestConfig'
